document = _{
  SOI ~ expression* ~ ws ~ EOI
}

expression = _{
  comment | property | list
}

comment = { 
  ws ~ "#" ~ comment_text
}

comment_text = @{
  ( !NEWLINE ~ ANY )*
}

value = {
	ws ~ primitive
}

property = { 
  ws ~ primitive ~ ws ~ operator ~ ws ~ primitive
}

list = { 
  keyable_list | array_list
}

keyable_list = _{
	ws ~ primitive ~ ws ~ operator ~ ws ~ "{" ~ expression* ~ ws ~ "}"
}

array_list = _{
	ws ~ primitive ~ ws ~ operator ~ ws ~ "{" ~ ( value | comment | keyless_list )* ~ ws ~ "}"
}

keyless_list = {
  ws ~ "{" ~ expression* ~ ws ~ "}"
}

primitive = {
  ( percentage | date | float | integer | boolean | string )
}

operator = @{
	( "=" | ">=" | "<=" | ">" | "<" )
}

date = @{
  ASCII_DIGIT{1,4} ~ "." ~ ASCII_DIGIT{1,2} ~ "." ~ ASCII_DIGIT{1,2} 
}

numeric = @{
  ASCII_DIGIT+
}

float = @{
  ( ( ( "-"? ~ numeric )? ~ "." ~ numeric ) | ( "-"? ~ numeric ~ "." ~ numeric? ) ) ~ &whitespace_character
}

integer = @{ 
  "-"? ~ numeric ~ &reserved_character
}

boolean = @{
   ( "yes" | "no" ) ~ &reserved_character
}

string = @{
	empty_string | quoted_string | unquoted_string
}

unquoted_string = {
	( ( ( "@" | "_"+ ) ~ ASCII_ALPHANUMERIC ) | ASCII_ALPHANUMERIC ) ~ ( !( reserved_character ) ~ ANY )*
}

quoted_string = @{
	( "\"" ~ (!"\"" ~ ANY)* ~ "\"" )
}

empty_string = @{
	"\"\""
}

percentage = {
  ( numeric ~ "%"+ )
}

reserved_character = _{
  whitespace_character | operator | "{" | "}" | "#"
}

whitespace_character = _{
	" " | "\t" | "\r" | "\n"
}

ws = {
  whitespace_character* ~ !whitespace_character
}
